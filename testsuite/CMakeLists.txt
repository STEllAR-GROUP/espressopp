set(Boost_LIBS ${Boost_LIBRARIES})
find_package(Boost COMPONENTS unit_test_framework REQUIRED)

list(APPEND Boost_LIBS ${Boost_LIBRARIES})
set(Boost_LIBRARIES ${Boost_LIBS})

include_directories(${Boost_INCLUDE_DIRS})

include_directories(${CMAKE_CURRENT_SOURCE_DIR}/../src/include)

if(USE_GCOV)
  set(PY_COV_OPTS "-m" "coverage" "run" "-a")
endif()

file(GLOB ALL_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/*)
foreach(DIR ${ALL_DIRS})
    if(IS_DIRECTORY ${DIR})
        if (EXISTS "${DIR}/IGNORE")
            continue()
        endif()
        if (EXISTS "${DIR}/CMakeLists.txt")
            add_subdirectory(${DIR})
        else()
            file(GLOB_RECURSE TESTS "${DIR}/*Test*.cpp")
            add_definitions(-DBOOST_TEST_DYN_LINK)
            foreach(TEST_FILE ${TESTS})
                get_filename_component(TEST_NAME ${TEST_FILE} NAME_WE)
                get_filename_component(TEST_FILENAME ${TEST_FILE} NAME)
                add_executable(${TEST_NAME} ${TEST_FILE})
                add_test(${TEST_NAME} ${CMAKE_CURRENT_BINARY_DIR}/${TEST_NAME})
                set_tests_properties(${TEST_NAME} PROPERTIES ENVIRONMENT "${TEST_ENV}")
                target_link_libraries(${TEST_NAME} ${PYTHON_LIBRARIES} MPI::MPI_CXX ${FFTW3_LIBRARIES} _espressopp ${Boost_LIBRARIES})
            endforeach()

            file(GLOB_RECURSE TESTS_PY "${DIR}/*Test*.py")
            foreach(TEST_FILE ${TESTS_PY})
                get_filename_component(TEST_NAME ${TEST_FILE} NAME_WE)
                add_test(${TEST_NAME} ${Python3_EXECUTABLE} ${PY_COV_OPTS} ${TEST_FILE})
                set_tests_properties(${TEST_NAME} PROPERTIES ENVIRONMENT "${TEST_ENV}")
            endforeach()
        endif()
    endif()
endforeach()
